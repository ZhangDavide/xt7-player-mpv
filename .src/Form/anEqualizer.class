' Gambas class file

Private tempdir As String
Public delaytimer As Timer

Public Sub switchers_Click()
  Dim child As Object
  For Each child In Last.parent.children
    If child <> Last Then
      child.enabled = Last.value
    Endif
  Next
End

Public Sub Form_Show()

End

Public Sub mpveq_mykill(retcode As Integer)
  Dim picfile As String
  Try picfile = tempdir & "/" & Dir(tempdir)[0]
  If Error Then Return
  Try picturebox1.picture = Picture.Load(picfile)
End

Public Function eqstring() As String
  Dim band_containers As New Object[]
  Dim band_container, subobj As Object
  Dim f, g, w, out_band, outstring As String
  Dim out_bands As New String[]
  Dim i As Integer
  
  band_containers.Add(frameband1)
  band_containers.Add(frameband2)
  band_containers.Add(frameband3)
  band_containers.Add(frameband4)
  For Each band_container In band_containers
    For Each subobj In band_container.children
      If subobj Is SwitchButton Then
        If subobj.value = True Then
          f = subobj.next.value
          g = subobj.next.next.value
          w = subobj.next.next.next.value
          out_bands.Add("f=" & f & " w=" & w & " g=" & g & " t=0")
        Endif
      Endif
    Next
  Next
  
  outstring = "[aid1]anequalizer="
  'Pretend we have 10 channels audio (unused channels should be discarded by anequalizer)
  For i = 0 To 2
    For Each out_band In out_bands
      outstring &= "c" & i & " " & out_band & "|"
    Next 'out_band
  Next 'i
  outstring = Left(outstring, -1)
  outstring &= ":\"curves=true:fscale=0:size=" & picturebox1.w & "x" & picturebox1.h & ":mgain=30\"[v1][vo]"
  Return outstring
End

Public Sub DrawGraph()
  Dim mpveq As MplayerClass
  If Not Global.initdone Then Global.init() 
  tempdir = Global.tmpdir & "/anequalizer"
  If Not Exist(tempdir) Then 
    Try Mkdir tempdir
    If Error Then Return
  Endif
  mpveq = New MplayerClass As "mpveq"
  mpveq.mpvoptions["demuxer"] = "rawaudio"
  mpveq.mpvoptions["lavfi-complex"] = "\"" & eqstring() & "\""
  mpveq.mpvoptions["vo"] = "image"
  mpveq.mpvoptions["vo-image-format"] = "png"
  mpveq.mpvoptions["vo-image-outdir"] = tempdir
  mpveq.mpvoptions["frames"] = "1"
  mpveq.do_play("/dev/random")
End

Private lastobj As Object

Public Sub delaytimer_Timer()
  Dim lastvalue As String = lastobj.value
  Wait 0.05
  If (lastvalue = lastobj.value) Then DrawGraph()
End


Public Sub EqControls_Change()
    delaytimer = New Timer As "delaytimer"
    lastobj = Last
    delaytimer.trigger
End


Public Function parse() As String
  Return "\"" & eqstring() & "\""
End

