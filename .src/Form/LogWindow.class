' Gambas class file

'Copyright (C) 2007, 2008 Antonio Orefice
' Gambas class file



Public Sub Form_Open()
'logarea.text = File.Load("/tmp/text")
  Global.Center(Fmain, Me)
End

Public Sub OkBTN_Click()
  Me.hide
  Wait 0.1
  If FMain.mplayer.ProcessRunningOvr() Then FMain.VideoBox.SetFocus()
End

Public Sub RefreshBTN_Click()
  ShowLogs()
  searchterm_Click()
  searchterm2_Click()
End

Public Sub Form_Show()
 ShowLogs()
 searchterm.SetFocus()
End


Public Sub clearlogs()
  Try logarea.Clear
  Try errorarea.clear
End

Public Struct mpvOption 
  name As String
  data As String
End Struct

Public Sub ShowLogs()
  Dim name, data As String

  streamtextbox.text = FMain.mplayer.getP("fullpath")
  Logarea.Clear
  errorarea.clear
  
  logarea.text &= "Commandline:\n"
  logarea.text &= FMain.mplayer.commandline & "\n\n"
  
  logarea.text &= "Options:\n"

  For Each FMain.mplayer.mpvOptions
    name = FMain.mplayer.mpvOptions.Key
    logarea.text &= "  --" & name
    data = FMain.mplayer.mpvOptions[name]
    If data <> "" Then logarea.text &= "=" & data
    logarea.text &= "\n"
  Next
  logarea.text &= "\n"
  
  logarea.text &= FMain.mplayer.stdout
  errorarea.text &= FMain.mplayer.stderr
End




Public Sub ScrollTo(text As String, logarea As Textedit)
  Dim p As Integer
  p = InStr(logarea.text, text, CInt(logarea.tag + 1), gb.case)
  If (p = 0) And (logarea.tag <> 0) Then 
    logarea.tag = 0 
    p = InStr(logarea.text, text, CInt(logarea.tag + 1))
  Endif
  logarea.scrolly = 9999999999
  logarea.scrollx = 9999999999
  logarea.pos = p
  logarea.EnsureVisible
  logarea.scrolly -= 20
  logarea.scrollx -= 20
  Wait
  logarea.tag = p
End



Public Sub searchterm_Click()
  HighLight(logarea, Last.text)
  ScrollTo(Last.text, logarea)
End


Public Sub searchterm2_Click()
  HighLight(errorarea, Last.text)
End


Public Sub Searchterm_Keyrelease()
  Dim previous_search As String = searchterm.text
  If (key.code = Key.return) Or (Key.code = Key.enter) Then
    ScrollTo(searchterm.text, logarea)
    Return
  Endif

  Wait 0.5
  If previous_search <> searchterm.text Then Return
  HighLight(logarea, Last.text)
End


Public Sub Searchterm2_Keyrelease()
  HighLight(errorarea, Last.text)
End


Public Sub Highlight(logarea As Object, searchterm As String)
  Dim sx, sy As Integer
  ' sx = logarea.scrollx
  ' sy = logarea.scrolly
  
  If (Trim(Last.text) = "") Then 
    Last.background = Color.Background
    Return
  Endif
  If DoHighlight(logarea, searchterm) Then
    Last.background = Color.Background
      Else
    Last.background = Color.red
  Endif
  ' logarea.scrollx = sx
  ' logarea.scrolly = sy
End


Private Function DoHighlight(logarea As Object, foundstr As String) As Boolean
  Dim lenfoundstr As Integer = 0
  Dim rtext As String
  Dim hlight1 As String = "<font color=red><b>"
  Dim hlight2 As String = "</b></font>"
  Dim i, sfound As Integer
  Dim found As Boolean = False
  Dim MyLeft, MyMid, MyRight As String
  lenfoundstr = Len(foundstr)
  logarea.EnsureVisible
  rtext = logarea.text
  i = 0  
  rtext = Replace(logarea.text, "\n", "<br>", gb.IgnoreCase)
  Repeat
    sfound = InStr(rtext, foundstr, i, gb.IgnoreCase)
    If sfound = 0 Then Break
    myleft = Left(rtext, sfound - 1) 
    mymid = hlight1 & Mid(rtext, sfound, lenfoundstr) & hlight2
    myright = Mid(rtext, sfound + lenfoundstr, Len(rtext))
    rtext = myleft & mymid & myright
    i = Len(myleft) + Len(mymid) + 1
    found = True
  Until (sfound = 0) Or (i >= Len(rtext))

  logarea.RichText = rtext  
  Return found
End



